// ========================================
// MapleExpectation Multi-Module Root Project
// ADR-014/ADR-017: Gradle 멀티모듈 구조
// ========================================

plugins {
	id 'java'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id 'org.gradle.test-retry' version '1.6.0'
	id 'com.github.spotbugs' version '6.0.25'
	id 'com.diffplug.spotless' version '6.25.0'
	id 'idea'
}

group = 'maple'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

// All projects: 공통 설정
allprojects {
	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'com.diffplug.spotless'

	group = 'maple'
	version = '0.0.1-SNAPSHOT'

	repositories {
		mavenCentral()
	}

	dependencyManagement {
	    imports {
	        mavenBom "org.springframework.boot:spring-boot-dependencies:3.5.4"
	        mavenBom "io.github.resilience4j:resilience4j-bom:2.2.0"
	        mavenBom "org.testcontainers:testcontainers-bom:1.20.4"
	    }
	}

	spotless {
		java {
			googleJavaFormat()
			formatAnnotations()
			importOrder()
			removeUnusedImports()
			trimTrailingWhitespace()
			endWithNewline()
		}
	}
}

// Subprojects: 모듈별 공통 설정
subprojects {
	apply plugin: 'jacoco'
	apply plugin: 'org.gradle.test-retry'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}

	// 공통 의존성
	dependencies {
		compileOnly 'org.projectlombok:lombok:1.18.30'
		annotationProcessor 'org.projectlombok:lombok:1.18.30'
		testCompileOnly 'org.projectlombok:lombok:1.18.30'
		testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

		testImplementation('org.springframework.boot:spring-boot-starter-test') {
			exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
		}

		testImplementation "org.testcontainers:testcontainers"
		testImplementation "org.testcontainers:junit-jupiter"
		testImplementation "org.testcontainers:mysql"
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

		// ArchUnit
		testImplementation 'com.tngtech.archunit:archunit:1.3.0'

		// Awaitility
		testImplementation 'org.awaitility:awaitility:4.2.0'
	}

	// 테스트 설정
	boolean isCiServer = System.getenv().containsKey("CI")

	test {
		jvmArgs = [
			'-Xms512m',
			'-Xmx2048m',
			'-XX:+UseG1GC',
			'-XX:MaxMetaspaceSize=512m'
		]

		useJUnitPlatform {
			if (project.hasProperty('fastTest')) {
				excludeTags 'sentinel', 'slow', 'quarantine', 'chaos', 'nightmare', 'integration', 'flaky'
			} else if (project.hasProperty('integrationTest')) {
				includeTags 'integration'
			} else if (project.hasProperty('chaosTest')) {
				includeTags 'chaos'
			} else if (project.hasProperty('nightmareTest')) {
				includeTags 'nightmare'
			} else {
				excludeTags 'sentinel', 'quarantine', 'flaky'
			}
		}

		retry {
			if (isCiServer) {
				maxRetries = 1
				maxFailures = 5
				failOnPassedAfterRetry = false
			}
		}

		systemProperty 'flaky.logging.enabled', 'true'
		systemProperty 'flaky.log.dir', "${buildDir}/flaky"

		maxParallelForks = 1
		environment "DOCKER_HOST", "unix:///var/run/docker.sock"

		finalizedBy jacocoTestReport

		testLogging {
			events "passed", "skipped", "failed"
			exceptionFormat "full"
		}
	}

	jacoco {
		toolVersion = "0.8.11"
	}

	jacocoTestReport {
		dependsOn test
		reports {
			xml.required = true
			html.required = true
		}
	}
}

// SpotBugs (루트 프로젝트에서만)
spotbugs {
	ignoreFailures = false
	showStackTraces = true
}

spotbugsMain {
	enabled = false
}

spotbugsTest {
	enabled = false
}
